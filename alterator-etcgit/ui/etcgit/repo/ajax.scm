(define-module (ui etcgit repo ajax)
  :use-module (srfi srfi-1)
  :use-module (alterator ajax)
  :use-module (alterator plist)
  :use-module (alterator woo)
  :export (init))

(define (format-row row proc)
  (if (plist? row)
    (let* ((ns-vs (plist-fold
                    (lambda (n v ns-vs)
                      (cons
                        (append (car ns-vs) (list n))
                        (append (cdr ns-vs) (list v))))
                    (cons '() '())
                    row))
           (res (apply proc (cdr ns-vs)))
           (ns (car ns-vs))
           (vs (list-head res (length ns)))
           (aux (list-tail res (length vs))))
      (append
        (fold (lambda (n v rlist)
                (append rlist (list n v)))
              '()
              ns
              vs)
        aux))
    (cons (car row) (format-row (cdr row) proc))))

(define (format-branch name label remote_head local_head status pstatus)
  (append 
    (list name label remote_head local_head
        (case (string->symbol status)
          ((nw) (_ "remote only"))
          ((lo) (_ "local only"))
          ((ff) ( _ "remote outdated"))
          ((fr) (_ "local outdated"))
          ((eq) (_ "equal"))
          ((br) (_ "derived"))
          (else (_ "unknown")))
        (case (string->symbol pstatus)
          ((eq) (_ "published"))
          ((ff) (_ "outdated"))
          ((fr) (_ "derived"))
          ((br) (_ "derived"))
          ((nw) (_ "public only"))
          ((lo) (_ "unpublished"))
          (else (_ "unpublished"))))
    (case (string->symbol status)
      ((nw) (list 'class "A"))
      ((fr) (list 'class "A"))
      ((ff) (list 'class "M"))
      ((br) (list 'class "M"))
      ((lo) (list 'class "D"))
      ((un) (list 'class "D"))
      (else '()))))

(define (read-branches)
  (let ((url (form-value "url")))
    (if (and url (not (string-null? url)))
      (catch/message
        (lambda ()
          (form-update-enum "branches" (map (lambda (row)
                                              (format-row row format-branch))
                                            (woo-list "/etcgit/branches" 'url url))))))))

(define (read-publication)
  (catch/message
    (lambda ()
      (let ((publication-data (woo-read-first "/etcgit/publication")))
        (form-update-value "publication-status" (if (woo-get-option publication-data 'status) "on" "off"))
        (form-update-visibility "publication-info" (woo-get-option publication-data 'status))
        (form-update-value "publicurl" (woo-get-option publication-data 'url))))))

(define (read-repo)
  (catch/message
    (lambda ()
      (let ((branch-data (woo-read-first "/etcgit/branch")))
        (form-update-value "url" (woo-get-option branch-data 'url "")))))
  (read-publication))

(define (update-buttons)
  (let* ((selected? (not (null? (string-split (or (form-value "branches") "") #\;)))))
    (form-update-activity "update-selected" selected?)
    (form-update-activity "publish-selected" selected?)
    (form-update-activity "delete-selected" selected?)))

(define (write-publication-status)
  (catch/message
    (lambda ()
      (woo-write "/etcgit/publication" 'status (form-value "publication-status"))))
  (read-publication))

(define (init)
  (form-bind "fetch" "click" read-branches)
  (form-bind "branches" "change" update-buttons)
  (form-bind "publication-status" "change" write-publication-status)
  (read-repo)
  (read-branches)
  (update-buttons))
